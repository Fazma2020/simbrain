apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'application'
apply plugin: 'checkstyle'

def versionName = "Simbrain3.1Beta"
def jmonkeyengine_version = '[3.2,)'
def log4jVer = '2.5'
def dl4j_version = "1.0.0-beta"

mainClassName = 'org.simbrain.workspace.gui.Splasher'

sourceCompatibility = 1.9

sourceSets {
    main {
        java {
            srcDir 'src'
        }
        resources {
            srcDirs 'src/org/simbrain/resource',
                    'src/org/simbrain/world/odorworld/resources',
                    'src/org/simbrain/world/threedworld/threedassets/',
                    'src/org/simbrain/custom_sims/resources/'
            exclude '**/*.java'
        }
    }
}

compileJava {
    // https://discuss.gradle.org/t/regarding-the-annotation-processors-on-compile-classpath-warning-in-gradle-4-6/26144
    options.compilerArgs += '-proc:none'
    options.encoding = 'UTF-8'
}

task pathingJar(type: Jar) {
    dependsOn configurations.runtime
    appendix = 'pathing'

    doFirst {
        manifest {
            attributes "Class-Path": configurations.runtime.files.collect {
                it.toURL().toString().replaceFirst(/file:\/+/ , '/' )
            }.join(' ')
            attributes (
                'Main-Class': 'org.simbrain.workspace.gui.SimbrainDesktop'
            )
        }

    }
}

run {
    dependsOn pathingJar
    doFirst {
        classpath = files(sourceSets.main.output.files, pathingJar.archivePath)
    }
}

checkstyle{
    configFile rootProject.file('etc/checkstyle.xml')
}


// Define a custom simbrain distribution task. Invoke with <code>gradle sb_distZip</code>
distributions {
    sb_ {
        description 
        baseName = versionName
        contents {
            from {
                uberJar
            }
            from {projectDir} {
                include 'scripts/**'
                include 'etc/Readme.txt'
                include 'etc/ReleaseNotes.txt'
                include 'etc/License.txt'
                include 'etc/config.properties'
                include 'simulations/**'
                include 'utils/**'
                include 'docs/**'
            }
        }
    }

}

repositories {
    mavenCentral()
    jcenter()
}
dependencies {

    // TODO: Remove these dependencies
    compile files(
            'lib/ojalgo-35.0.jar',
            'lib/utils.jar',
            'lib/ssj.jar')

    // https://mvnrepository.com/artifact/log4j/log4j
    compile group: 'log4j', name: 'log4j', version: '1.2.16'

    // https://mvnrepository.com/artifact/junit/junit
    compile "junit:junit:4.12"

    // https://mvnrepository.com/artifact/com.thoughtworks.xstream/xstream
    compile group: 'com.thoughtworks.xstream', name: 'xstream', version: '1.4.10'

    // https://mvnrepository.com/artifact/org.piccolo2d/piccolo2d-extras
    compile group: 'org.piccolo2d', name: 'piccolo2d-extras', version: '3.0'
    compile group: 'org.piccolo2d', name: 'piccolo2d-core', version: '3.0'

    // https://mvnrepository.com/artifact/jfree/jfreechart
    compile group: 'jfree', name: 'jfreechart', version: '1.0.12'

    // TODO: Migrate to nd4j
    // https://mvnrepository.com/artifact/org.jblas/jblas
    compile group: 'org.jblas', name: 'jblas', version: '1.2.4'
    // https://mvnrepository.com/artifact/gov.nist.math/jama
    compile group: 'gov.nist.math', name: 'jama', version: '1.0.2'

    // Dl4j, ND4j.
    // compile "org.deeplearning4j:deeplearning4j-core:${dl4j_version}"
    compile "org.nd4j:nd4j-native-platform:${dl4j_version}"

    // https://mvnrepository.com/artifact/org.swinglabs/swingx-core
    compile group: 'org.swinglabs', name: 'swingx-core', version: '1.6.2-2'

    // https://mvnrepository.com/artifact/com.miglayout/miglayout-swing
    compile group: 'com.miglayout', name: 'miglayout-swing', version: '5.0'

    // https://mvnrepository.com/artifact/com.fifesoft/rsyntaxtextarea
    compile group: 'com.fifesoft', name: 'rsyntaxtextarea', version: '2.5.3'

    // https://mvnrepository.com/artifact/org.beanshell/bsh
    compile group: 'org.beanshell', name: 'bsh', version: '2.0b5'

    // JME. Only one of bullet or bullet-native should be uncommented
    compile "org.jmonkeyengine:jme3-core:$jmonkeyengine_version"
    compile "org.jmonkeyengine:jme3-desktop:$jmonkeyengine_version"
    compile "org.jmonkeyengine:jme3-jogl:$jmonkeyengine_version"
    compile "org.jmonkeyengine:jme3-plugins:$jmonkeyengine_version"
    compile "org.jmonkeyengine:jme3-effects:$jmonkeyengine_version"
    compile "org.jmonkeyengine:jme3-networking:$jmonkeyengine_version"
    compile "org.jmonkeyengine:jme3-blender:$jmonkeyengine_version"
    // compile "org.jmonkeyengine:jme3-bullet:$jmonkeyengine_version"
    compile "org.jmonkeyengine:jme3-bullet-native:$jmonkeyengine_version"
    compile "org.jmonkeyengine:jme3-lwjgl:$jmonkeyengine_version"
    compile "org.jmonkeyengine:jme3-jogg:$jmonkeyengine_version"
    compile "org.jmonkeyengine:jme3-niftygui:$jmonkeyengine_version"
    // compile "com.badlogicgames.gdx:gdx-ai:1.8.0"
    compile "javax.vecmath:vecmath:1.5.2"

    // TODO: Not sure why the thing below didn't work in the format above...
    // Maybe try again from command line starting with gradle clean.  Intellij not quite working
    // https://mvnrepository.com/artifact/org.jmonkeyengine/jme3-jogg/3.2.0-stable
    compile group: 'org.jmonkeyengine', name: 'jme3-jogg', version: '3.2.0-stable'

}

// TODO: Create a custom distribution that loads up this together with other stuff
task uberJar(type: Jar) {

    archivesBaseName = versionName
    manifest {
        attributes(
                'Main-Class': mainClassName
        )
    }

    from sourceSets.main.output
    from configurations.runtimeClasspath.
            findAll { it.name.endsWith('jar') }.
            collect { zipTree(it) }
}

task printStuff {
    group 'Misc'
    description 'For printing variable bindings / learning gradle'
    doLast {
        println gradle.gradleHomeDir
        println mainClassName
        println project.buildDir
        println "" + project.buildDir + "/" + project.executableDir
        println rootProject
        println projectDir
    }
}